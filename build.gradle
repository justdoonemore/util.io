apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

group="com.jdom"
version="0.0.5"

configurations {
    // configuration shared by compile or testCompile and the custom ANT tasks
    sharedJibx
 
    compile {
        extendsFrom sharedJibx
     }
    testCompile {
        extendsFrom sharedJibx
    }
 
    // Dependencies needed for jibx binding
    jibxAntBind {extendsFrom sharedJibx}
}

repositories {
    maven {
        credentials {
           username nexusUsername
           password nexusPassword
        }
        url nexusUrl
    }
}

uploadArchives {
   repositories {
       mavenDeployer {
	       repository(url: releasesUrl) {
	           authentication(userName: nexusUsername, password: nexusPassword)
           }
       }
   }
}

/**
 * Task to bind classes unsing jibx.
 * depends on compileJava, as it needs the classes.
 */
task bind(dependsOn: 'compileJava') << {
    ant.echo("Binding code with jibx...")
    ant.taskdef(name: 'bind', classname: 'org.jibx.binding.ant.CompileTask', classpath: configurations.jibxAntBind.getAsPath())
 
    ant.bind(verbose: false, load: true, binding: "${projectDir}/src/main/config/binding.xml") {
        classpath {
            pathelement(path: sourceSets.main.output.classesDir)
            pathelement(path: configurations.jibxAntBind.getAsPath())
        }
    }
}

task testBind(dependsOn: 'compileTestJava') << {
    ant.echo("Binding test code with jibx...")
    ant.taskdef(name: 'bind', classname: 'org.jibx.binding.ant.CompileTask', classpath: configurations.jibxAntBind.getAsPath())
 
    ant.bind(verbose: false, load: true, binding: "${projectDir}/src/test/config/binding.xml") {
        classpath {
            pathelement(path: sourceSets.test.output.classesDir)
            pathelement(path: configurations.jibxAntBind.getAsPath())
        }
    }
}

dependencies {
   sharedJibx "org.jibx:jibx-run:1.1" 
   jibxAntBind "org.jibx:jibx-bind:1.1"
   jibxAntBind "bcel:bcel:5.1"
   jibxAntBind "xpp3:xpp3:1.1.3.3"
   
   compile "commons-io:commons-io:2.1"
   compile "junit:junit:4.11"
   compile "com.jdom:util.lang:0.0.5"
   compile "com.jdom:util.log.api:0.0.4"
   compile "javax.mail:mail:1.4.4"
   compile "commons-httpclient:commons-httpclient:3.1"
   
   testCompile "junit:junit:4.11"
   testCompile "org.powermock:powermock-module-junit4:1.5"
   testCompile "org.powermock:powermock-api-mockito:1.5"
}

// make sure that after compiling sources, binding is run.
tasks.getByPath('compileJava').doLast {
    tasks.getByPath('bind').execute()
}
// make sure that after compiling tests, binding is run.
tasks.getByPath('compileTestJava').doLast {
    tasks.getByPath('testBind').execute()
}